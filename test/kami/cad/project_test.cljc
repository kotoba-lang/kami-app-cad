(ns kami.cad.project-test (:require [clojure.test :refer [deftest is]] [kami.cad :as cad] [kami.cad.project :as project]))
(def sections [(cad/curve [[-2 0 0] [0 2 0] [2 0 0]] [1 1 1]) (cad/curve [[-2 0 2] [0 3 2] [2 0 2]] [1 1 1])])
(deftest round-trip
  (let [p (project/document {:id "loft" :name "Loft" :sections sections :tessellation 32 :selection {:section 1 :point 2}
                             :camera {:azimuth 1 :elevation 0.2} :precision {:snap 0.001 :sketch-width 5} :interaction {:profile :rhino}})]
    (is (project/valid? p)) (is (= p (project/open p))) (is (= 32 (:project/tessellation p)))))
(deftest legacy-migration (let [p (project/open sections)] (is (= sections (:project/sections p))) (is (= 16 (:project/tessellation p)))))
(deftest rejects-invalid
  (is (thrown? #?(:clj Exception :cljs js/Error) (project/open {:no :geometry})))
  (is (thrown? #?(:clj Exception :cljs js/Error) (project/open {:kami/document :cad-project :kami/version 99}))))

(deftest repairs-legacy-millimetre-snap-values
  (let [p (project/document {:id "legacy" :name "Legacy" :sections sections :tessellation 16
                             :selection {:section 0 :point 0} :camera {} :precision {:snap 0.5 :sketch-width 4}
                             :interaction {:profile :rhino}})]
    (is (= 0.0005 (get-in (project/open p) [:project/precision :snap])))))

(deftest migrates-version-two-for-feature-history
  (let [v3 (project/document {:sections sections :tessellation 16 :selection {} :camera {}
                              :precision {:snap 0.001} :interaction {}})
        migrated (project/open (-> v3 (assoc :kami/version 2) (dissoc :project/feature-model)))]
    (is (= 5 (:kami/version migrated)))
    (is (contains? migrated :project/feature-model))
    (is (nil? (:project/feature-model migrated)))
    (is (= :loft (:project/view-mode migrated)))
    (is (nil? (:project/solid migrated)))))

(deftest migrates-version-four-dimensional-sketch
  (let [v4 (-> (project/document {:sections sections :tessellation 16 :selection {} :camera {}
                                   :precision {:snap 0.001 :sketch-width 6.5} :interaction {}})
               (assoc :kami/version 4) (dissoc :project/sketch))
        migrated (project/open v4)]
    (is (= {:width 6.5 :height 2.0} (:project/sketch migrated)))))
